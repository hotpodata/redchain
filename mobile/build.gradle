apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        applicationId "com.hotpodata.redchain"
        targetSdkVersion 23
        versionCode 3
        versionName "1.0.2"
        multiDexEnabled true
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOGGING_ENABLED", "true"
            buildConfigField "boolean", "IS_DEBUG_BUILD", "true"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOGGING_ENABLED", "false"
            buildConfigField "boolean", "IS_DEBUG_BUILD", "false"
        }
    }
    signingConfigs {
        def provDir = System.getenv("REDCHAIN_KEYSTORE_DIRECTORY") ?: ""
        def redchainKeystorePass = System.getenv("REDCHAIN_KEYSTORE_PASS") ?: ""
        def redchainProPass = System.getenv("REDCHAIN_PRO_PASS") ?: ""
        redchainProRelease {
            storeFile file("${provDir}/redchain.keystore")
            storePassword redchainKeystorePass
            keyAlias "redchain-pro-release"
            keyPassword redchainProPass
        }

        def redchainFreePass = System.getenv("REDCHAIN_FREE_PASS") ?: ""
        redchainFreeRelease {
            storeFile file("${provDir}/redchain.keystore")
            storePassword redchainKeystorePass
            keyAlias "redchain-free-release"
            keyPassword redchainFreePass
        }
    }
    productFlavors {
        def freeSigning = signingConfigs.redchainFreeRelease
        def proSigning = signingConfigs.redchainProRelease
        Free {
            applicationId "com.hotpodata.redchain.free"
            buildConfigField "boolean", "IS_PRO", "false"
            signingConfig freeSigning
            resValue "string", "app_label", "RedChain"
        }
        Pro {
            applicationId "com.hotpodata.redchain.pro"
            buildConfigField "boolean", "IS_PRO", "true"
            signingConfig proSigning
            resValue "string", "app_label", "RedChain Pro"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}
task switchToFree(type: Copy) {
    description = 'Switches to FREE google-services.json'
    from "src/free"
    include "google-services.json"
    into "."
}

task switchToPro(type: Copy) {
    description = 'Switches to PRO google-services.json'
    from "src/pro"
    include "google-services.json"
    into "."
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.google.android.gms:play-services:8.3.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'joda-time:joda-time:2.8.2'
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'me.grantland:autofittextview:0.2.+'
}
buildscript {
    ext.kotlin_version = '1.0.0-beta-3595'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}

afterEvaluate {
    processFreeDebugGoogleServices.dependsOn switchToFree
    processFreeReleaseGoogleServices.dependsOn switchToFree
    processProDebugGoogleServices.dependsOn switchToPro
    processProReleaseGoogleServices.dependsOn switchToPro
}
